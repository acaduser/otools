(defun c:layAllOn(/ doc)
	(vl-load-com)
	(princ "\nðŸ’¡")
	(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
	(acet-layerp-mark t)
	(vlax-for lay (vla-get-Layers doc)
		(vla-put-LayerOn lay ':vlax-true)
	)
	(acet-layerp-mark nil)
	(vla-EndUndoMark doc)
	(princ)
)
(defun c:layAllOff(/ doc)
	(vl-load-com)
	(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
	(acet-layerp-mark t)
	(vlax-for lay (vla-get-Layers doc)
		(if (= (vla-get-Name lay) (getvar "clayer"))
			(vla-put-LayerOn lay ':vlax-true)
			(vla-put-LayerOn lay ':vlax-false)
		)
	)
	(acet-layerp-mark nil)
	(vla-EndUndoMark doc)
	(princ)
)
(defun c:laySSON (/ *error* main doc)
	(vl-load-com)
	(defun main()
		(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
		(if (setq ss (ssget))
			(progn
				(setq enames
					(vl-remove-if-not
						'(lambda(x)	(= (type x) 'ENAME))
						(mapcar 'cadr (ssnamex ss))
					)
				)
				(setq names
					(unique (vl-sort (mapcar '(lambda(x) (cdr(assoc 8 (entget x)))) enames) '<))
				)
				(acet-layerp-mark t)
				(vlax-for lay (vla-get-Layers doc)
					(setq name (vla-get-name lay))
					(if (not(member name names))
						(vla-put-LayerOn lay ':vlax-false)
					)
				)
				(acet-layerp-mark nil)
				(if (< (cdr (assoc 62 (entget (tblobjname "layer" (getvar "clayer"))))) 0)
					(progn
						(setvar "clayer" (car names))
						(princ (strcat "\nç¾åœ¨ã®ç”»å±¤ã¯ " (car names) " ã«è¨­å®šã•ã‚Œã¾ã—ãŸã€‚"))
					)
				)
			)
		)
		(vla-EndUndoMark doc)
		(princ)
	)
	(defun unique(lst)
		(append
			(apply
				'append
				(mapcar '(lambda(c d)(if(/= c d)(list c))) lst (cdr lst))
			)
			(list(last lst))
		)
	)
	(defun *error* (s)
		(princ s)
		(vla-EndUndoMark doc)
		(princ)
	)
	(main)
)
(defun c:laySSOff (/ *error* main doc)
	(vl-load-com)
	(defun main()
		(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
		(if (setq ss (ssget))
			(progn
				(setq enames
					(vl-remove-if-not
						'(lambda(x)	(= (type x) 'ENAME))
						(mapcar 'cadr (ssnamex ss))
					)
				)
				(setq layerList
					(unique (vl-sort (mapcar '(lambda(x) (cdr(assoc 8 (entget x)))) enames) '<))
				)
				(acet-layerp-mark t)
				(vlax-for lay (vla-get-Layers doc)
					(setq name (vla-get-name lay))
					(if (member name layerList)
						(vla-put-LayerOn lay ':vlax-false)
					)
				)
				(acet-layerp-mark nil)
			)
		)
		(vla-EndUndoMark doc)
		(princ)
	)
	(defun unique(lst)
		(append
			(apply
				'append
				(mapcar '(lambda(c d)(if(/= c d)(list c))) lst (cdr lst))
			)
			(list(last lst))
		)
	)
	(defun *error* (s)
		(princ s)
		(vla-EndUndoMark doc)
		(princ)
	)
	(main)
)
(defun c:layPickNestOn (/ *error* main doc)
	(vl-load-com)
	(defun main ()
		(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
		(sssetfirst)
		(if (setq pick (nentsel "\nã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆé¸æŠž:"))
			(progn
				(setq ename (car pick))
				(setq enameList (last pick))
				(setq name (cdr (assoc 8 (entget ename))))
				(if (and (= name "0") (= (type (car enameList)) 'ENAME))
					(progn
						(setq bname
							(car
								(vl-remove-if
									'(lambda(x)
										(= (cdr (assoc 8 (entget x))) "0")
									)
									enameList
								)
							)
						)
						(if bname
							(setq name (cdr (assoc 8 (entget bname))))
							(setq name "0")
						)
					)
				)
				(acet-layerp-mark t)
				(vlax-for lay (vla-get-Layers doc)
					(if (= (vla-get-name lay) name)
						(vla-put-LayerOn lay ':vlax-true)
						(vla-put-LayerOn lay ':vlax-false)
					)
				)
				(acet-layerp-mark nil)
				(print name)
			)
		)
		(vla-EndUndoMark doc)
		(princ)
	)
	(defun *error*(s)
		(princ s)
		(vla-EndUndoMark doc)
		(princ)
	)
	(main)
)
(defun c:layPickNestOff (/ *error* main doc)
	(vl-load-com)
	(defun main ()
		(vla-StartUndoMark (setq doc (vla-get-ActiveDocument (vlax-get-acad-object))))
		(sssetfirst)
		(if (setq pick (nentsel "\nã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆé¸æŠž:"))
			(progn
				(setq enameList (last pick))
				(setq obj (vlax-ename->vla-object (car pick)))
				(setq name (vla-get-Layer obj))
				(if (and (= name "0") (= (vla-get-ObjectName obj) "AcDbAttribute"))
					(setq name (vla-get-Layer (vla-ObjectIDToObject doc (vla-get-OwnerID obj))))
				)
				(if (= name "0")
					(if (= (type (car enameList)) 'ENAME)
						(progn
							(setq bname
								(car
									(vl-remove-if
										'(lambda(x)
											(= (cdr (assoc 8 (entget x))) "0")
										)
										enameList
									)
								)
							)
							(if bname
								(setq name (cdr (assoc 8 (entget bname))))
								(setq name "0")
							)
						)
					)
				)
				(acet-layerp-mark t)
				(vlax-for lay (vla-get-Layers doc)
					(if (= (vla-get-Name lay) name) (vla-put-LayerOn lay ':vlax-false))
				)
				(acet-layerp-mark nil)
				(print name)
			)
		)
		(vla-EndUndoMark doc)
		(princ)
	)
	(defun *error*(s)
		(princ s)
		(vla-EndUndoMark doc)
		(princ)
	)
	(main)
)
